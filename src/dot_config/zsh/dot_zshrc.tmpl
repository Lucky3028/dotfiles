typeset -U path PATH
path=(
  $path
  $HOME/bin(N-/)
  $HOME/.local/bin(N-/)
  $XDG_DATA_HOME/mise/shims(N-/)
  $CARGO_HOME/bin(N-/)
  # Add code.exe to PATH for WSL
  /mnt/c/Users/*/AppData/Local/Programs/Microsoft\ VS\ Code/bin(N-/)
  # Add pwsh.exe to PATH for WSL
  /mnt/c/Program\ Files/PowerShell/7(N-/)
)

typeset -U fpath FPATH
fpath=(
  $fpath
  $ZDOTDIR/completions(N-/)
  $MISE_TAB_COMP_PATH(N-/)
)

# Load mise
eval "$($MISE_INSTALL_PATH activate zsh)"

# Load sheldon
eval "$(sheldon source)"

{{ if eq .chezmoi.os "linux" }}

# Run a script when it is NOT running in a container
if ! env | grep -q container; then

  SSH_AGENT_FILE=~/.ssh/.ssh-agent
  BW_SESSION_FILE=~/.ssh/.bw-session

  if [ -f "$SSH_AGENT_FILE" ]; then
    source "$SSH_AGENT_FILE" > /dev/null
  fi
  if [ -z "$SSH_AGENT_PID" ] || ! kill -0 $SSH_AGENT_PID 2> /dev/null; then
    ssh-agent > "$SSH_AGENT_FILE"
    source "$SSH_AGENT_FILE" > /dev/null

    # BW_SESSION が未設定なら保存済みトークンをロード
    if [ -z "$BW_SESSION" ] && [ -f "$BW_SESSION_FILE" ]; then
      export BW_SESSION=$(< "$BW_SESSION_FILE")
    fi

    # vault が locked なら unlock してトークンを保存
    bw_status=$(bw status 2>/dev/null | jq -r '.status' 2>/dev/null)
    if [ "$bw_status" != "unlocked" ]; then
      echo "Bitwarden: vault is ${bw_status:-unknown}. Please unlock:"
      export BW_SESSION=$(bw unlock --raw)
      if [ -n "$BW_SESSION" ]; then
        install -m 600 /dev/null "$BW_SESSION_FILE"
        printf '%s' "$BW_SESSION" > "$BW_SESSION_FILE"
      fi
    fi

    # BW_SESSION が有効なら SSH 鍵を追加
    if [ -n "$BW_SESSION" ]; then
      bw list items 2>/dev/null | jq -c '.[] | select(.type == 5)' | while IFS= read -r item; do
        name=$(printf '%s' "$item" | jq -r '.name')
        keyfile=$(mktemp --tmpdir "XXXXXX_${name}")
        printf '%s' "$item" | jq -r '.sshKey.privateKey' > "$keyfile"
        chmod 600 "$keyfile"
        ssh-add "$keyfile" > /dev/null 2>&1
        rm -f "$keyfile"
      done
    fi
  fi

fi

{{ end }}
